metadata author = {
	name: 'Stas Sultanov'
	urls: {
		gitHub: 'https://github.com/stas-sultanov'
		linkedIn: 'https://www.linkedin.com/in/stas-sultanov'
	}
}

/* parameters */

@description('Name of the Cdn/profiles resource.')
param Cdn_profiles__name string

@description('Name of the Network/dnsZones resource.')
param Network_dnsZones__name string

@description('Id of the Network/frontDoorWebApplicationFirewallPolicies resource.')
param Network_frontDoorWebApplicationFirewallPolicies__id string

@description('List of paths to apply security policy.')
param cdn_profiles_securityPolicies__PatternsToMatch string[] = [
	'/*'
]

@description('Name of the resource.')
param name string = 'apex'

@description('Time to live of the A record in seconds.')
@maxValue(86400)
@minValue(1)
param network_dnsZones_a__TTL int = 3600

@description('Time to live of the validation TXT record in seconds.')
@maxValue(86400)
@minValue(1)
param network_dnsZones_txt__TTL int = 3600

@description('Tags to put on the resources.')
param tags object

/* variables */

var cdn_profiles_customDomains__name = replace(
	Network_dnsZones_.name,
	'.',
	'-'
)

var cdn_profiles_customDomains__hostName = Network_dnsZones_.name

var network_dnsZones_txt_Validation_name = '_dnsauth'

var network_frontDoorWebApplicationFirewallPolicies__id_split = split(
	Network_frontDoorWebApplicationFirewallPolicies__id,
	'/'
)

/* existing resources */

resource Cdn_profiles_ 'Microsoft.Cdn/profiles@2025-04-15' existing = {
	name: Cdn_profiles__name
}

resource Network_dnsZones_ 'Microsoft.Network/dnsZones@2018-05-01' existing = {
	name: Network_dnsZones__name
}

resource Network_frontDoorWebApplicationFirewallPolicies_ 'Microsoft.Network/FrontDoorWebApplicationFirewallPolicies@2025-03-01' existing = {
	name: network_frontDoorWebApplicationFirewallPolicies__id_split[8]
	scope: resourceGroup(
		network_frontDoorWebApplicationFirewallPolicies__id_split[2],
		network_frontDoorWebApplicationFirewallPolicies__id_split[4]
	)
}

/* resources */

// https://learn.microsoft.com/azure/templates/microsoft.cdn/profiles/afdendpoints
resource Cdn_profiles_afdEndpoints_ 'Microsoft.Cdn/profiles/afdEndpoints@2025-04-15' = {
	location: 'global'
	name: name
	parent: Cdn_profiles_
	properties: {
		autoGeneratedDomainNameLabelScope: 'ResourceGroupReuse'
	}
	tags: tags
}

// https://learn.microsoft.com/azure/templates/microsoft.cdn/profiles/customdomains
resource Cdn_profiles_customDomains_ 'Microsoft.Cdn/profiles/customDomains@2025-04-15' = {
	name: cdn_profiles_customDomains__name
	parent: Cdn_profiles_
	properties: {
		azureDnsZone: {
			id: Network_dnsZones_.id
		}
		hostName: cdn_profiles_customDomains__hostName
		tlsSettings: {
			certificateType: 'ManagedCertificate'
			minimumTlsVersion: 'TLS12'
		}
	}
}

// https://learn.microsoft.com/azure/templates/microsoft.cdn/profiles/securitypolicies
resource Cdn_profiles_securityPolicies_ 'Microsoft.Cdn/profiles/securityPolicies@2025-04-15' = {
	name: name
	parent: Cdn_profiles_
	properties: {
		parameters: {
			associations: [
				{
					domains: [
						{
							id: Cdn_profiles_customDomains_.id
						}
					]
					patternsToMatch: cdn_profiles_securityPolicies__PatternsToMatch
				}
			]
			type: 'WebApplicationFirewall'
			wafPolicy: {
				id: Network_frontDoorWebApplicationFirewallPolicies_.id
			}
		}
	}
}

// https://learn.microsoft.com/azure/templates/microsoft.network/dnszones/a
resource Network_dnsZones_a_ 'Microsoft.Network/dnsZones/A@2018-05-01' = {
	name: '@'
	parent: Network_dnsZones_
	properties: {
		TTL: network_dnsZones_a__TTL
		targetResource: {
			id: Cdn_profiles_afdEndpoints_.id
		}
	}
}

// https://learn.microsoft.com/azure/templates/microsoft.network/dnszones/txt
resource Network_dnsZones_txt_ 'Microsoft.Network/dnsZones/TXT@2018-05-01' = {
	name: network_dnsZones_txt_Validation_name
	parent: Network_dnsZones_
	properties: {
		TTL: network_dnsZones_txt__TTL
		TXTRecords: [
			{
				value: [
					Cdn_profiles_customDomains_.properties.validationProperties.validationToken
				]
			}
		]
	}
}

/* outputs */

output Cdn_profiles_afdEndpoints__name string = Cdn_profiles_afdEndpoints_.name

output Cdn_profiles_customDomains__name string = Cdn_profiles_customDomains_.name

output Cdn_profiles_securityPolicies__id string = Cdn_profiles_securityPolicies_.id

output Network_dnsZones_a__name string = Network_dnsZones_a_.name

output Network_dnsZones_txt__name string = Network_dnsZones_txt_.name
